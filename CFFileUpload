<cffunction name="CFFileUpload" returntype="string" access="public" output="false">
	<cfargument name="deleteBadFiles" type="boolean" required="false" default="true">
	<cfset var loc = {}>
	<cfset loc.filetypes = {}>
	<!--- pdf --->
	<cfset loc.filetypes.pdf = "application/pdf,application/x-pdf,app/pdf">
	<!--- office --->
	<cfset loc.filetypes.ppt = "application/vnd.ms-powerpoint">
	<cfset loc.filetypes.xls = "application/vnd.ms-excel">
	<cfset loc.filetypes.doc = "application/msword">
	<cfset loc.filetypes.pub = "application/x-mspublisher,application/vnd.ms-publisher">
	<cfset loc.filetypes.docm = "application/vnd.ms-word.document.macroEnabled.12">
	<cfset loc.filetypes.docx = "application/vnd.openxmlformats-officedocument.wordprocessingml.document">
	<cfset loc.filetypes.dotm = "application/vnd.ms-word.template.macroEnabled.12">
	<cfset loc.filetypes.dotx = "application/vnd.openxmlformats-officedocument.wordprocessingml.template">
	<cfset loc.filetypes.potm = "application/vnd.ms-powerpoint.template.macroEnabled.12">
	<cfset loc.filetypes.potx = "application/vnd.openxmlformats-officedocument.presentationml.template">
	<cfset loc.filetypes.ppam = "application/vnd.ms-powerpoint.addin.macroEnabled.12">
	<cfset loc.filetypes.ppsm = "application/vnd.ms-powerpoint.slideshow.macroEnabled.12">
	<cfset loc.filetypes.ppsx = "application/vnd.openxmlformats-officedocument.presentationml.slideshow">
	<cfset loc.filetypes.pptm = "application/vnd.ms-powerpoint.presentation.macroEnabled.12">
	<cfset loc.filetypes.pptx = "application/vnd.openxmlformats-officedocument.presentationml.presentation">
	<cfset loc.filetypes.xlam = "application/vnd.ms-excel.addin.macroEnabled.12">
	<cfset loc.filetypes.xlsb = "application/vnd.ms-excel.sheet.binary.macroEnabled.12">
	<cfset loc.filetypes.xlsm = "application/vnd.ms-excel.sheet.macroEnabled.12">
	<cfset loc.filetypes.xlsx = "application/vnd.openxmlformats-officedocument.spreadsheetml.sheet">
	<cfset loc.filetypes.xltm = "application/vnd.ms-excel.template.macroEnabled.12">
	<cfset loc.filetypes.xltx = "application/vnd.openxmlformats-officedocument.spreadsheetml.template">
	<!--- images --->
	<cfset loc.filetypes.jpg = "image/jpg,image/pjpg,image/jpeg,image/pjpeg">
	<cfset loc.filetypes.jpeg = "image/jpg,image/pjpg,image/jpeg,image/pjpeg">
	<cfset loc.filetypes.gif = "image/gif">
	<cfset loc.filetypes.png = "image/png">

	<!--- the result to use internally --->
	<cfset arguments.result = "loc.cffile">
	<!---
		intercept the upload destination and set it to the tempdirectory,
		save it though so we can move the file later
	 --->
	<cfset loc.destination = arguments.destination>
	<cfset arguments.destination = getTempDirectory()>

	<!--- execute upload --->
	<cffile attributeCollection="#arguments#">

	<!--- get the full filename that was uploaded --->
	<cfset loc.fileuploaded = arguments.destination & loc.cffile["serverFile"]>

	<!--- make sure the file uploaded was the proper mimetype from the extension --->
	<cfset loc.filemimetype = getPageContext().getServletContext().getMimeType(loc.fileuploaded)>
	<cfif
		not structkeyexists(loc.filetypes, loc.cffile["serverFileExt"])
		or not listfindnocase(loc.filetypes[loc.cffile["serverFileExt"]], loc.filemimetype)>
		<cfif arguments.deleteBadFiles>
			<cffile action="delete" file="#loc.fileuploaded#">
		</cfif>
		<cfthrow type="Custom" message="Invalid file type" detail="#loc.filemimetype#">
	</cfif>

	<cfset loc.destination = listappend(loc.destination, loc.cffile["serverFile"], "\/")>
	<cffile action="MOVE" source="#loc.fileuploaded#" destination="#loc.destination#" nameConflict="OVERWRITE">

	<cfreturn loc.cffile>
</cffunction>